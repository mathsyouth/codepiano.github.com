---
layout: post
keywords: Julia
description: Julia 语言学习总结
title: Julia 语言学习笔记
categories: [Julia]
tags: [Julia]
group: archive
icon: rocket
---
{% include mathsyouth/setup %}


### Basics

#### Help
Prompt: `help>`

By typing ? at the beginning of a line, you can enter the help mode. Entering anything searches the Julia documentation for that word:

```julia
    help?> sin
    search: sin sinh sind sinc sinpi asin using isinf asinh asind isinteger isinteractive sign signif signed

  sin(x)

  Compute sine of x, where x is in radians.
```

#### Key bindings
to exit the REPL, you can use `^D` (Ctrl+D)

#### Using `println()`

The `println` function prints the string representation of an object.

```julia
    julia> println("Hello, Julia!")
    Hello, Julia!
```

#### Using string concatenation

String concatenation is just a fancy name for putting strings together. In this case, we create two variables that represent strings, then use the string() function to put them together. The `string()` function concatenates each of its positional arguments.

```julia
    julia> what = "Hello"
    "Hello"

    julia> whom = "Julia"
    "Julia"

    julia> string(what, ", ", whom, "!")
    "Hello, Julia!"
```

Julia also allows for variables to be called within string literals. So the above is equivalent to:

```julia
    julia> "$what, $whom\!"
    "Hello, Julia!"
```

And this is true even for maths (or any function!)

```julia
    julia> "2 plus 2 is $(2+2)."
    "2 plus 2 is 4."
```

#### Running Julia programs

To run a Julia program, you have two options – either include it in another program or the REPL, or specify it as a positional argument in the command line.

Let's open a file in our favourite text editor, call it `hello.jl` (the commonly accepted file name for a Julia program), and enter

```julia
    println("hello world")
```

We now have two ways to launch it.

By opening the REPL, we can `include` the file. This will evaluate everything in the file, then return the result of the last valid expression.

```julia
    julia> include("hello.jl")
    "Hello, Julia!"
```

Alternatively, you can merely grace your command line with your greeting to Julia by launching Julia with the appropriate argument:

```julia
    $ julia hello.jl
    "Hello, Julia!"
```

Finally, you can use the shebang comment at the beginning of a file to make it shell executable. For instance, you could write:

```julia
#!/usr/bin/env julia

println("Hello, Julia\!")
```

This then allows you to execute your file from the command line by simply typing `./hello.jl` (provided that the file is executable - you may need to use `chmod` to make it so!).

### Literals

_Literals_ are ways to enter various data types, whether into the REPL or a program. 

#### Strings

To enter a string literal, simply delimit it by `"` (double quotation marks). Unlike Python or JavaScript, Julia does **not** accept single quotation marks. 

#### One-dimensional arrays

1D array literals are delimited by square brackets (`[]`). Each element they contain has to be either a variable or an otherwise valid literal, but they do not all have to be the same type (something true for most collections within Julia). Values are separated by a `,` (comma):

```julia
    julia> arr1 = [1, 2, "sausage", π]
    4-element Array{Any,1}:
         1
         2
          "sausage"
         π = 3.1415926535897...
```

#### Ranges

A range, in Julia, is simply a shorthand for a sequence of numbers that are all spaced equally. A range can be created using the `range()` function as `range(start, end)`, but it is usually denoted in a shorthand literal, `start:end`. Ranges are _lazy_, meaning that the values are not immediately available until they are needed.  You can, however, force the generation of the values by using the `collect()` function.

```julia
    julia> 0:5
    0:5

    julia> typeof(0:5)
    UnitRange{Int64}

    julia> collect(0:5)
    6-element Array{Int64,1}:
     0
     1
     2
     3
     4
     5
```

A range may have an optional middle `step` attribute. To obtain an array of the numbers from 0 to 30 in steps of 10:

```julia
    julia> collect(0:10:30)
    4-element Array{Int64,1}:
      0
     10
     20
     30
```

Because range is largely implemented as [Collections](https://docs.julialang.org/en/stable/stdlib/collections),  you can use most of the convenience functions:

```julia
    julia> for i in 1:3 println("$i great") end
    1 great
    2 great
    3 great

    julia> 2 in 1:10
    true

    julia> length(1:10)
    10

    julia> sum(1:10)
    55

    julia> @time reduce(+, 1:100000)
      0.000013 seconds (86 allocations: 6.170 KiB)
    5000050000
```

#### Multidimensional arrays

To create a multidimensional array, separate values by empty space and rows by a `;` (semicolon):

```julia
    julia> md_array = [1 1 2 3 5; 8 13 21 34 55; 89 144 233 377 610]
    3x5 Array{Int64,2}:
      1    1    2    3    5
      8   13   21   34   55
     89  144  233  377  610
```

Alternatively, you can enter columns, using square brackets (remember not to let your ingrained reflexes from Python take over and put a comma between the arrays!):

```julia
    julia> [[1, 2], [3, 4]]
    2-element Array{Array{Int64,1},1}:
     [1, 2]
     [3, 4]
    
    julia> [[1, 2] [3, 4]]
    2×2 Array{Int64,2}:
     1  3
     2  4
```

When entering a multidimensional array, each row has to have the same length. Failing to do so raises an error:

```julia
    julia> md_sparse_array = [1 1 2; 8 13 21 34 55]
    ERROR: ArgumentError: argument count does not match specified shape (expected 6, got 8)
```

#### Tuples

Tuples are unlike array literals in that they are immutable – once assigned, you cannot change the values in a tuple. Tuples are delimited by `()` (round brackets) and values are separated by commas.

```julia
    julia> fibo_tuple = (1, 1, 2, 3, 5)
    (1,1,2,3,5)
```

#### Dicts

Dicts are what in some other languages are known as _associative arrays_ or _maps_: they contain key-value pairs. A dict is delimited by round brackets. Individual mappings (key-value pairs) are separated by commas. Keys are separated from values by `=>` (a double-arrow).

```julia
    julia> statisticians = Dict("Gosset" => "1876-1937", "Pearson" => "1857-1936", "Galton" => "1822-1911")
    Dict{String,String} with 3 entries:
      "Galton"  => "1822-1911"
      "Pearson" => "1857-1936"
      "Gosset"  => "1876-1937"
```

#### Sets

Sets are similar to other collections in that they contain various values. However, unlike arrays and tuples, they are unordered and unique-constrained: no element may occur multiple times within the set. 

Sets do not have a specific notation, but rather are created using a syntax we will use a great deal for creating all kinds of objects: by using the type (`Set`) and entering the values to constitute the set in round brackets _and_ square braces:

```julia
    julia> stooges = Set(["Moe", "Curly", "Larry"])
    Set(String["Moe","Larry","Curly"])
```

Sets do accept duplicates at time of construction, but the resulting set will still only contain one of each unique element:

```julia
    julia> beatles = Set(["Lennon", "McCartney", "Harrison", "Starr", "Lennon"])
    Set(String["Starr","Harrison","McCartney","Lennon"])
```

Sets are unordered, meaning that two sets containing the same elements are equal:

```julia
    julia> Set(["Marsellus", "Jules", "Vincent"]) == Set(["Jules", "Vincent", "Marsellus"])
    true
```

An empty set is created by

```julia
    julia> Set([])
    Set{Any}()
```

### Julia's type system

A type system describes a programming language's way of handling individual pieces of data and determining how to operate on them based on their type. Julia's type system is primarily _dynamic_, meaning that there is no need to tell Julia what type a particular value is. 

Julia's dynamic system is augmented by the ability to specify types where needed. This has two advantages. First, type specification leads to more efficient code. It will make your code more stable, much faster and much more robust. At the same time, unlike in a statically typed language, you do not need to get your head around types at the very beginning. 

It's important to understand that in Julia, _types belong to values, not variables_. It's also important to understand the hierarchy of types in Julia.

Types may be _abstract_ or _concrete_. You can think of an abstract type as a type that is intended solely to act as supertypes of other types, rather than types of particular objects. Thus, no object that is not a type can have an abstract type as its type.

Concrete types are intended to be the types of actual objects. They are always subtypes of abstract types. This is because concrete types cannot have subtypes, and also because you can't create types that don't have supertypes (`Any` is the default supertype for any type you create). Here is useful to mention an interesting property of Julia's type system: any two types always have a common ancestor type.

Julia's primary type operator is `::` (double-colons). It has three different uses, all fairly important, and it's crucial that you understand the different functions that `::` fulfills in the different contexts.

#### Declaring a (sub)type

In the context of a _statement_, such as a function, `::` appended to a variable means 'this variable is always to be of this type'. In the following, we will create a function that returns `32` as `Int8`.

```julia
    julia> function restrict_this_integer()
            x::Int8 = 32
            x
           end
    restrict_this_integer (generic function with 1 method)

    julia> p = restrict_this_integer()
    32

    julia> typeof(p)
    Int8
```

As we can see, the `::` within the function had the effect that the returned result would be represented as an 8-bit integer (`Int8`). Recall that this _only works in the context of a statement_ – thus simply entering `x::Int8 = 32` will yield a error `ERROR: syntax: type declarations on global variables are not yet supported`.

#### Asserting a type

In every other context, `::` means 'I assert this value is of this particular type'. This is a great way to check a value for both abstract and concrete type.

For instance, you are provided a variable `input_from_user`. How do you make sure it has the right kind of value?

```julia
    julia> input_from_user = 128
    128

    julia> input_from_user::Integer
    128
    
    julia> input_from_user::Int
    128
    
    julia> input_from_user::Int8
    ERROR: TypeError: typeassert: expected Int8, got Int64
```

Remember that a type hierarchy is like a Venn diagram. Every `Int64` (a concrete type) is also an `Ìnteger`(an abstract type). Therefore, asserting `input_from_user::Int64` will also yield `128`. `Int` is just an alias for `Int64`.

#### Specifying acceptable function inputs

In Julia, you have the possibility to make sure your function only accepts values that you want it to. Consider creating a function that adds up only floating point numbers:

```julia
    function addition(x::Float64, y::Float64)
      x + y
    end
```

Calling it on two floating-point numbers will, of course, yield the expected result:

```julia
    julia> addition(3.14, 2.71)
    5.85
```

But giving it a simpler task will raise an error:

```julia
    julia> addition(1, 1)
    ERROR: MethodError: no method matching addition(::Int64, ::Int64)
```

The real meaning of this error is a little complex, and refers to one of the base features of Julia called _multiple dispatch_. In Julia, you can create multiple functions with the same name that process different types of inputs, so e.g. an `add()` function can add up `Int` and `Float` inputs but concatenate `String` type inputs. Multiple dispatch effectively creates a table for every possible type for which the function is defined and looks up the right function at call time. 

```julia
    function addition(x::Int, y::Int)
      x + y
    end
    
    julia> addition(1, 1)
    2
    
    function addition(x::String, y::String)
      string(x, y)
    end
    
    julia> addition("Hello, ", "World!")
    "Hello, World!"
```

#### Getting the type of a value

To obtain the type of a value, use the `typeof()` function. `typeof()` is notable for treating tuples differently from most other collections. Calling `typeof()` on a tuple enumerates the types of each element, whereas calling it on, say, an `Array` value returns the `Array` notation of type (which looks for the largest common type among the values, up to `Any`):

```julia
    julia> typeof([1, 2, "a"])
    Array{Any,1}

    julia> typeof((1, 2, "a"))
    Tuple{Int64,Int64,String}
```

Helpfully, the `isa()` function tells us whether something is a particular type:

```julia
    julia> isa("River", String)
    true
```

And, of course, types have types (specifically, `DataType`)!

```julia
    julia> typeof(ans)
    DataType
```

#### Exploring the type hierarchy

The `<:` operator can help you find out whether the left-side type is a subtype of the right-side type.

```julia
    julia> Int64 <: Integer
    true

    julia> Int <: Int64
    true
    
    julia> Int8 <: Int8
    true
    
    julia> Int <: Int8
    false
```

To reveal the supertype of a type, use the `supertype()` function:

```julia
    julia> supertype(String)
    AbstractString
```

#### Composite types

Composite types, known to C coders as `structs`, are more complex object structures that you can define to hold a set of values. For instance, to have a Type that would accommodate geographic coordinates, you would use a composite type. Composite types are created with the `struct` keyword:

```julia
    struct GeoCoordinates
        lat::Float64
        lon::Float64
    end
```

We can then create a new value with this type:

```julia
    julia> home = GeoCoordinates(51.7519, 1.2578)
    GeoCoordinates(51.7519,1.2578)

    julia> typeof(home)
    GeoCoordinates
```

The values of a composite object are, of course, accessible using the dot notation you might be used to from many other programming languages:

```julia
    julia> home.lat
    51.7519
```

It is not possible to assig a new value to `home.lat`. So if we would instantiate the immutable `GeoCoordinates` type with the values above, then attempt to change one of its values, we would get an error:

```julia
    julia> home.lat = 51.75
    ERROR: type GeoCoordinates is immutable
```

#### Creating your very own immutable

A _mutable_ type is one which, once instantiated, can be changed. They are created the same way as composite types, except by using the `mutable` keyword in front of `struct`:

```julia
    mutable struct GeoCoordinatesMut
        lat::Float64
        lon::Float64
    end
```

Once instantiated, you can change the values.  However, these values have to comply with the type's definition in that they have to be _convertible_ to the type specified (in our case, `Float64`). So, for instance, an `Int64` input would be acceptable, since you can convert an `Int64` into a `Float64` easily. On the other hand, a `String` would not do, since you cannot convert it into an `Int64`.

#### Type unions

Sometimes, it's useful to have a single alias for multiple types. To do so, you can create a _type union_ using the constructor `Union`:

```julia
    julia> Numeric = Union{Int, Float64}
    Union{Float64, Int64}

    julia> 1::Numeric
    1

    julia> 3.14::Numeric
    3.14
```

`+` is the 'shorthand' for over a hundred methods. You can see all of these by calling `methods()` on `+`:

```julia
    julia> methods(+)
    # 180 methods for generic function "+":
    +(x::Bool, z::Complex{Bool}) in Base at complex.jl:232
    +(x::Bool, y::Bool) in Base at bool.jl:89
    +(x::Bool) in Base at bool.jl:86
```

collections may be _mutable_ or _non-mutable_. Put quite simply, a mutable collection is one where you can change particular values after creation, while in an immutable collection, you cannot do so. `Sets` again represent a special case – they are what could be best described as _pseudo-immutable_ – there is no way to access values in a way that could change them.


|             |    **Mutable**     |   **Immutable**   |
|:-----------:|:------------------:|:-----------------:|
|  **Indexable**      | Arrays             | Tuples            |
|  **Associative**    | Dicts              |                   |
|  **Non-indexable and non-associative**   |       | Sets      |

### Indexable collections

#### Access

Elements of an indexable collection can be accessed using the square bracket notation, by their ordinal,  begins with 1, rather than 0. A range of numbers is written as `start:end` or `start:steps:end`. You can use a range to access a range of elements:

```julia
    julia> prime_array = [2, 3, 5, 7, 11]
    5-element Array{Int64,1}:
      2
      3
      5
      7
     11

    julia> prime_array[2:3]
    2-element Array{Int64,1}:
     3
     5
     
    julia> prime_array[1:2:5]
    3-element Array{Int64,1}:
     2
     5
     11
```

A range always returns a collection, even if it has the length 1. This is exemplified by the difference between `prime_array[3]`, the call we made above, and `prime_array[3:3]`, which returns

```julia
    julia> prime_array[3:3]
    1-element Array{Int64,1}:
     5

    julia> prime_array[3:3] == 5
    false
```

You can access the last element of an indexable collection using `[end]`:

```julia
    julia> prime_array[end]
     11
```

Incidentally, `end` behaves like a number – so `prime_array[end-1]` returns the penultimate element of the collection.

#### Setting

If the indexable collection you are using is also mutable (e.g. an array), any of these methods will act as a pseudo-variable and will allow you to assign a value to it. Thus `prime_array[end] = 12` would change the last element of `prime_array` to 12. You also aren't restricted to a single element: calling `prime_array[2:4] = 0` would result in

```julia
    julia> prime_array
    5-element Array{Int64,1}:
      2
      0
      0
      0
     11
```

And, of course, you can use an array or another indexable collection to replace values:

```julia
    julia> prime_array[2:4] = [3,5,7]
    3-element Array{Int64,1}:
     3
     5
     7

    julia> prime_array
    5-element Array{Int64,1}:
      2
      3
      5
      7
     11
```

#### Unpacking

Indexable collections can _unpack_: that is, they can be assigned in a single line to as many distinct variables as they have elements. This is a very useful convenience feature, and is much used in functional programming:

```julia
    julia> actors = ["Ian McKellen", "Martin Freeman", "Elijah Wood"];

    julia> gandalf, bilbo, frodo = actors
    3-element Array{String,1}:
     "Ian McKellen"
     "Martin Freeman"
     "Elijah Wood"

    julia> gandalf
    "Ian McKellen"
```

Unpacking can also be used to swap the contents of variables:

```julia
    julia> firstname = "Irving";
    julia> lastname = "Washington";

    julia> firstname, lastname = lastname, firstname
    ("Washington","Irving")

    julia> lastname
    "Irving"
```

#### `push!`, `pop!` and `append!`

`push!` appends the value to the end of the collection. `pop!` takes the last element of the list, returns it and removes it from the collection.

```julia
    julia> array = [1,2,3,4];

    julia> push!(array, 5)
    5-element Array{Int64,1}:
     1
     2
     3
     4
     5

    julia> pop!(array)
    5

    julia> array
    4-element Array{Int64,1}:
     1
     2
     3
     4
```

`append!`, somewhat unusually, puts the elements of a collection to the end of another collection:

```julia
    julia> array2 = [5,6,7];

    julia> append!(array, array2)
    7-element Array{Int64,1}:
     1
     2
     3
     4
     5
     6
     7
```

#### `shift!` and `unshift!`

`shift!` and `unshift!` are the front equivalent of `pop!` and `push!`.
Similarly to `pop!`, `shift!` retrieves the first element of the collection and removes it from the collection (which *shifts* it). Similarly to `push!`, `unshift!` puts an element to the front of the collection:

#### `find` functions

There's a set of functions starting with find — such as `find()`, `findfirst()`, and `findnext()` — that you can use to get the index or indices of values within an indexable collection that match a specific value, or pass a test. These functions share three properties.

1. Their result is the _index or indices of the value sought or tested for_, with the _n\_th_ element's index being _n_, not _n-1_ as you might be used to from other languages.
2. You can use these functions in two principal forms: you can test for a value or you can test for a function, in which case the results will be the values for which the function returns `true`.
3. The `find` functions' way of telling you they haven't found anything is returning zero, since there is no element of index zero.

Let's try to find things within the following `Array`: `primes_and_one = [1,2,3,5,7,11,13,17,19,23]`

#### `findfirst()`

`findfirst()` finds the first occurrence of a value and returns its index (or zero):

```julia
    julia> findfirst(primes_and_one, 5)
    4
```

As noted above, we can feed the `find` functions a function as well – it will return values for which the function would return a `true` value.  A function of the form `x -> x == 13` results in `true` if the value of `x` is 13 and `false` otherwise. Let's try to see which prime number is the first to equal 13:

```julia
    julia> @time findfirst(x -> x == 13, primes_and_one)
      0.013266 seconds (1.30 k allocations: 71.637 KiB)
    7

    julia> @time findfirst(primes_and_one, 13)
      0.000004 seconds (4 allocations: 160 bytes)
    7
```

You might have noticed that unlike in the case where you were searching for a particular value, _where you're searching by a function, the function comes first_. 

#### `find()`

`find()` returns an array of results. Thus, for instance, let's use the `isinteger` function to see which of our primes are integers:

```julia
    julia> find(isinteger, primes_and_one)
    10-element Array{Int64,1}:
      1
      2
      3
      4
      5
      6
      7
      8
      9
     10
```

#### `findnext()`

`findnext()` returns results from a given index onwards. Thus, if you want to know the index of the first odd number after 3 in the list of primes, you would proceed as follows (using the function `isodd`, which, as you could guess, returns `true` for odd integers and `false` otherwise):

```julia
    julia> findnext(isodd, primes_and_one, 4)
    4
```

As you might have noticed, when you use a function as an argument, you do not use the parentheses you would normally use to call a function. That is because `function()` means `call this function` while `function` is merely a reference to the function object itself.

#### Get elements, not indices

So far, we've only been getting indices. How do we get the actual elements? The answer is, of course, by using our magical `[]`(square brackets) syntax. 

```julia
    julia> primes_and_one[find(isodd,primes_and_one)]
    9-element Array{Int64,1}:
      1
      3
      5
      7
     11
     13
     17
     19
     23
```

#### Filtering

The `filter()` function works quite similar to `find`, except in this case returns only the elements that satisfy the condition (it is, effectively, a shorthand for the previous listing).

```julia
    julia> @time filter(isodd, primes_and_one)
      0.000163 seconds (8 allocations: 512 bytes)
    9-element Array{Int64,1}:
      1
      3
      5
      7
     11
     13
     17
     19
     23
     
    julia> @time primes_and_one[find(isodd, primes_and_one)]
      0.000402 seconds (11 allocations: 848 bytes)
```

`filter()` can be used in-place, by using the `!` after the name of the function. Thus, using `filter!()`, alters the actual array rather than returning a filtered copy. Note, however, that functions ending with `!` modify the object, so, obviously, the type they act on must be mutable – you would, therefore, not be able to `filter!()` a tuple, even though you would be able to `filter()` it.

#### Sorting

The `sort()` function sorts an array lexicographically, generally in an ascending order:

```julia
    julia> sort([-3, 2, 1, 7])
    4-element Array{Int64,1}:
     -3
     1
     2
     7
```

You can specify the sort criterion using `by` – in this case, we will be using the absolute value function `abs`:

```julia
    julia> sort([-3,2,1,7], by=abs)
    4-element Array{Int64,1}:
      1
      2
     -3
      7
```

You can change the order of sorting using `rev`:

```julia
    julia> sort([-3,2,1,7], by=abs, rev=true)
    4-element Array{Int64,1}:
      7
     -3
      2
      1
```

And, for the great joy of algorithm nerds like me, you can choose the sort algorithm using `alg`. Julia currently supports three sorting algorithms (`InsertionSort`, `QuickSort` and `MergeSort`).

```julia
    julia> sort([-3,2,1,7], by=abs, rev=true, alg=MergeSort)
    4-element Array{Int64,1}:
      7
     -3
      2
      1
```

For mutable indexable collections, such as arrays, you can use `sort!()`, which sorts 'in place'. Of course, you can also sort non-numeric elements, or indeed anything for which the `isless()` function is defined, which sorting uses internally.

```julia
    julia> sort(["Bayes", "Laplace", "Poisson", "Gauss"])
    4-element Array{String,1}:
     "Bayes"
     "Gauss"
     "Laplace"
     "Poisson"
```

#### Counting

`count()` tells you the number of instances in the collection that satisfy the criterion:

```julia
    julia> count(isodd, primes_and_one)
    9
```

#### `all()` and `any()`

`all()` and `any()` implement two of the mathematical concepts known as _quantifiers_, with `all()` representing the universal quantifier `\\forall`, while `any()` implements the existential quantifier. These functions test whether all or any, respectively, of a collection satisfies a certain criterion, and return a single truth value.

#### Existence of a particular value

To find out whether an array has a particular value among its elements, you can use `in()`:

```julia
    julia> in(2, 1:5)
    true
```

Somewhat strangely, in the `in()` syntax, the needle comes before the haystack, i.e. `in(value, array)`, where `value` denotes the value you are looking for.

## Particular types

### Arrays

Arrays (the ones we used in our examples so far in this section) are mutable indexable collections. The type `Array{T,N}` indicates an `N`-dimensional array which elements' types are subtypes of `T`. For instance, `Array{Number, 2}` is an 2-dimensional array. Its elements' types descend from `Number` (e.g.`Int`, `Float64`).

#### Access in multidimensional arrays

How do we access elements in a multidimensional array, a special form of indexable collection? Simple – in a multidimensional array, indexes go down each row, then from left to right. Therefore, this array

```julia
    julia> md_array = ["A" "B"; "C" "D"]
    2x2 Array{String,2}:
     "A"  "B"
     "C"  "D"
```

would be indexed as follows:

    md_array[1] = "A"
    md_array[2] = "C"
    md_array[3] = "B"
    md_array[4] = "D"

This is a little counterintuitive and different from the usual row/column notation, where you would use `array[row][column]`. To retrieve a cell by row and column, use `array[row, column]`:

```julia
    julia> md_array[1,2]
    "B"
```

This generalizes for higher dimension arrays.

### Tuples

Tuples are generally used for small fixed-length collections — they're ubiquitous across Julia, for example as argument lists. Where a function returns multiple values, which, as we see, is pretty often the case, the result is a tuple.

A corollary of immutability is that none of the `!` functions work on tuples - but at the same time, using functions such as `push()` is perfectly acceptable, since it returns _a copy of the tuple with the added element_, which does not alter the original tuple.

### Associative collections: dicts

An associative collection is a kind of non-indexed collection that stores (usually) pairs of values. The indexable collections you have encountered correspond to real-life examples such as a shopping list or a sequential list of train stations. Associative collections, on the other hand, have a _key_ and a _value_ (for this reason, they are sometimes referred to as _key-value pairs_). Julia, like many other programming languages, implements associative collections in an object called a `dict` (short for `dictionary`), which corresponds to 'maps', 'hash tables' or 'dictionaries' in other programming languages.

A dict, as we have seen, is usually created using the dict literal

```julia
    dict = Dict("a" => 1, "b" => 2, "c" => 3)
```

The key of a key-value pair is _unique_, meaning that while several keys might point at the same value (and a key might point at a collection as a value), you cannot have duplicate keys.

#### Creating dicts

```julia
    Dict(i => sqrt(i) for i = 1:2:15)
```

You can also create an empty dictionary. `Dict()` will construct an empty dictionary that permits any elements, while `Dict{type1, type2}()` will create an empty dictionary that permits any elements with keys of `type1` and values of `type2`.

#### Access

Just like items in an indexable arrays are keyed by their index, items in a dict are identified by their key and retrieved using the square bracket syntax:

```julia
    statisticians = Dict("Gosset" => "1876-1937", "Pearson" => "1857-1936", "Galton" => "1822-1911")

    julia> statisticians["Gosset"]
    "1876-1937"
```

One drawback of the bracket syntax is that if there is no entry for the key provided, Julia will raise an error. An alternative form of accessing a dictionary is using the `get()` function, which accepts a default value:

```julia
    julia> get(statisticians, "Pearson", "I'm sorry, I don't know when this person lived.")
    "1857-1936"

    julia> get(statisticians, "Kendall", "I'm sorry, I don't know when this person lived.")
    "I'm sorry, I don't know when this person lived."
```

An advantage of this is that you can create a default value, which the function will return if it cannot find the key requested. Unlike in some other programming languages, a default is _not optional_ for Julia.

#### Get or create (`get!()`)

Because dicts are mutable, `get!()` can try to access a value by its key and create it if not found, then return the new value. Its syntax is identical to `get()`:

```julia
    get!(statisticians, "Kendall", "I'm sorry, I don't know when this person lived.")
```

#### `pop!()`

`pop!()` returns the key-value array matching the key. If the key does not exist, it returns an optional default value or throws an error.

#### Change values

To change a value, access it via the bracket syntax, then assign it the new value:

```julia
    statisticians["Kendall"] = "1907-1983"
```

#### Checking for existence of a key or a key-value pair

To check for the existence of a key without retrieving it, you can use `haskey()`. You can also check for the existence of a key-value pair in a dict using the `in()` function you might be familiar with from arrays. The notation of a key-value pair in this case will be in the form of the associative array symbol `=>`:

```julia
    julia> in("Bayes" => "1702-1761", statisticians)
    false
```

#### Retrieving keys or values

To retrieve all keys of a dict, use `keys()`. This will retrieve an object of type `Base.KeyIterator`, which does just what the name suggests - it iterates through the keys of an array. You can retrieve values, predictably, by using the `values()` function.

#### Sorting

You may have noticed that dicts are unordered. This is because dicts are not indexable, therefore there is no ordering that would make inherent sense. However, sometimes, we like dictionaries sorted. Therefore, you have to write your own sort function that first converts `statisticians` from a dict into an array of 2-element tuples. This is because the `sort()` function has no defined methods for dicts, but it can sort arrays, including tuples, where it sorts by the first element in the tuple. 

#### Merging

The function `merge()` merges two or more dicts.

```julia
    mathematicians = Dict("Gauss" => "1777-1855", "Leibniz" => "1646-1716", "Abel" => "1802-1829")
    merge(mathematicians, statisticians)
```

Its bang counterpart, `merge!()`, merges them in place, overwriting the first dict mentioned while leaving the second intact.


### Non-indexable non-associative collections: sets

You might be familiar with the idea of sets from maths/set theory. A set is a non-indexable, non-associative and non-mutable collection that also has unique elements. No element may occur twice, so an element's value identifies it conclusively.

#### Creating sets

To create a set, use the `Set()` constructor function. You can create a set that accepts any data type

```julia
    primes = Set()
    primes = Set{Int64}()
    mersenne_primes_set = Set([3, 7, 31, 127])
```

#### Set operations

Sets have some unique functions that accommodate certain problems well-known from set theory: the functions `union()`, `intersect()` and `setdiff()` each, respectively, implement the union, intersection and difference of sets. Let's see how we can use this to find some similarities between the cast of two blockbusters, _The Lord of the Rings_ and _The Matrix_.

First, let's create two sets with some actors from each movie:

```julia
    lotr_actors = Set(["Elijah Wood", "Ian McKellen", "Viggo Mortensen", "Hugo Weaving"])
    matrix_actors = Set(["Keanu Reeves", "Lawrence Fishburne", "Hugo Weaving"])
```

To find shared actors, we can use `intersect()`:

```julia
    intersect(lotr_actors, matrix_actors)
```

To find actors who only starred in _Lord of the Rings_ but not in _The Matrix_, we can use `setdiff()`, which shows all elements that are in the first `Set` but not the second. Finally, we can see actors who played in either of the movies, by using `union()`.

### Collections and types

Until now, we have generally created collections using literals, and with precious little regard to the types of information that go in them. While types will be discussed in quite a bit of detail later on, what we do know about types is that they are individual categories of data.

Julia operates what is called _type inference_: unless you tell it explicitly what type something is, it tries to figure it out best as it can. We see this in operation when we create a new collection. 

```julia
    mersenne_primes = [3, 7, 31, 127, 8191, 131071]
```

Upon creating the array, the REPL reports to us that it's an array consisting of six elements, all of type `Int64` – a type of signed 64-bit integer. It also, helpfully, reports to us that we've got a 1-dimensional array.

#### Type inference and dissimilar types

What, however, if I want to play it a little wild and mix it up? Consider the following array:

```julia
     julia> not_really_mersenne_primes = [3, 7, "potato", 127, π, "wallaby"]
     6-element Array{Any,1}:
          3
          7
           "potato"
          127
          π = 3.1415926535897...
          "wallaby"
```

As you have guessed, Julia is at a loss as to what to do with this, since we've got a mix of integers, strings and a constant thrown in for good measure. Therefore, it tells us that it has inferred the type of the collection to be `Any` – a type that applies to all objects.


### 参考文献

1. [Learn Julia the hard way!](https://github.com/chrisvoncsefalvay/learn-julia-the-hard-way)


